!*******************************************************
!********** CERN Benchmarking **************************
!*******************************************************

/title, udf_dipole
/prep7

!********** Options *******************************
!*afun,rad
pi=acos(-1)
*afun,deg
!/UIS,MSGPOP,4  !suppress warnings
/NERR,,99999999,     ! changes # of error and warnings to prevent crashing for large # of warnings
!**************************************************

/title, udf_dipole


!**************************************************
!****** Material Properties (SI - magnetic) *******
!**************************************************

!Coil
mp,murx,1,1   !rel. perm.
mp,rsvx,1,(1.0e-8)/pi      ! from Lorenzo

!Air
mp,murx,10,1   !rel. perm.

!Iron (4)  bh table from cern
tb,bh,4,, 44
TBPT,,0.000000,0.000000 
TBPT,,7.941832,0.007120 
TBPT,,15.883663,0.016700 
TBPT,,23.825495,0.029250 
TBPT,,31.767326,0.045220 
TBPT,,47.531623,0.088910 
TBPT,,63.367539,0.154340 
TBPT,,79.219371,0.240660 
TBPT,,158.812757,0.765070 
TBPT,,317.649387,1.296830 
TBPT,,475.180947,1.452170 
TBPT,,633.627647,1.520430 
TBPT,,792.106179,1.558410 
TBPT,,1192.094374,1.609540 
TBPT,,1589.758907,1.638920 
TBPT,,1987.081257,1.660550 
TBPT,,2386.281635,1.678260 
TBPT,,2782.290957,1.694270 
TBPT,,3179.493941,1.708280 
TBPT,,3577.039107,1.721750 
TBPT,,3971.377302,1.734140 
TBPT,,4765.210312,1.757060 
TBPT,,5559.099026,1.778050 
TBPT,,6354.069994,1.797640 
TBPT,,7149.486595,1.815670 
TBPT,,7944.083548,1.833300 
TBPT,,10590.000000,1.850000 
TBPT,,13160.000000,1.900000 
TBPT,,21170.000000,2.000000 
TBPT,,33760.000000,2.100000 
TBPT,,66000.000000,2.200000 
TBPT,,120960.000000,2.280000 
TBPT,,169600.000000,2.344300 
TBPT,,212170.000000,2.399600 
TBPT,,283130.000000,2.490500 
TBPT,,339890.000000,2.562700 
TBPT,,425040.000000,2.670600 
TBPT,,566950.000000,2.849800 
TBPT,,850760.000000,3.207400 
TBPT,,1702300.000000,4.278200 
TBPT,,2128000.000000,4.813400 
TBPT,,2837700.000000,5.705200 
TBPT,,3405100.000000,6.418600 
TBPT,,4256700.000000,7.488700
TBPLOT,bh,4,


!**********EM Elements*******************************
et,1,plane53        !2d EM: Az
keyopt,1,1,0    
!et,2,user102   !2d EM: Az,cur,emf (stranded circu)   
!**************************************************

et,2,user102   !2d EM: Az,cur,emf (stranded circu)   
keyopt,2,1,2   ! circuit coupled
keyopt,2,2,1   ! transfer to thermal with binary (0=no,1=yes)
keyopt,2,3,1   ! LBNL Jc fit -> THIS IS NOT YET SET UP -> HARD CODE SCU FIT, key(3) does nothing
               ! no keyopt 4
keyopt,2,5,0   ! table/fixed for rsvx (not applicable since  key(7)=1)
               ! no keyopt 6
keyopt,2,7,2   ! force qflag at all times (0=check, 1=SC, 2=Quenched)
keyopt,2,8,1   ! IFCC, (0=yes,calc tau; 1=no; 3=yes with fixed tau from real 11)



!/input,02_dipole_geo_twoairbc_dense,txt,
/input,02_dipole_geo_twoairbc_loop,txt,

!name conductor 
cmsel,s,cond1
*do,i,2,nc
	cmsel,a,cond%i%
*enddo
alls,below,area
emodif,all,type,2
emodif,all,real,2




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! real constants
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! scaling to match inductance and resistance -> pick not the same
Lc = 10.11     
Li = 9.20      


!set real constants
nturns = nc
nstrand = 40
ds = 0.75e-3
fcond = nstrand*ds*ds*pi/(4*aw*bw)
fsc=0.4
Lp = 14e-3
feff = 0.75
RRR = 200
TauMult = 0.0
scIFCU = 1.0 
curdir = 1.0
Ctau = 1e-3     !if constant tau is chosen using keyopt

! Nb3Sn fit parameters (from Edvard)
Tc0 = 16.0
Bc0 = 28.11
Jc0 = 174000/Bc0
afit=0.96
pfit = 1.52

cmsel,s,cond
alls,below,area
asum
*get,across,area,all,area
		
!no real constants are required for this simulation, put anyways to check they set correct
R,2,across,nturns,fcond,fsc,curdir,Lc   !set real constants
Rmore,Li,RRR,Lp,feff,Ctau,TauMult
Rmore,Jc0,Tc0,Bc0,afit,pfit,scIFCU



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Set Up Circuit
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!et,9,circu124
!keyopt,9,1,4        ! circuit element - independent voltage source
!keyopt,9,2,4        ! piecewise linear load

et,5,circu124
keyopt,5,1,5        ! circuit element - stranded conductor -> connected to FEA domain

et,6,circu124
keyopt,6,1,0        ! circuit element - external resistor

et,3,circu124
keyopt,3,1,3        ! circuit element - independent current source
keyopt,3,2,4        ! piecewise linear load


!********** Real Constants for Circuit ************************************************************************
rdump = 32.05e-3
cur1 = 800

SymFac = 4  ! left/right,top/bot

R,3,cur1,0.0

R,5,1.0     !symfactor is 1 (no symmetry)
R,5,SymFac   
R,6,rdump
!*set,Rtot,rdump+SymFac*2*lmag*rhoc/(across)
!*set,Rtot,rdump+2*lmag*rhoc/(across)
*set,Rtot,rdump
R,9,0,cur1*RTot,5.0/1000,cur1*RTot,5.1/1000,0,1000/1000,0    ! "dump res switch" 


!set ramp parameters 
tf = 1.0
I0f = 1.0e2


R,3,0,0,tf,I0f    ! "linear ramp of current to I0f"




!**************************************************
!********** Create Circuit  ***********************
!**************************************************
csys,0

xcirc = x0
ycirc = -10e-3
dyy = 12e-3
xsft = aw*nc+dx*nc

!make circuit nodes (location is arbitrary, connectivity is important)
alls
numcmp,node
*get,nnode,node,,num,max
numstr,node,nnode
n,nnode+1,xcirc,ycirc
n,nnode+2,xcirc+xsft,ycirc
n,nnode+3,xcirc+xsft,ycirc-dyy
n,nnode+4,xcirc+xsft/2.0,ycirc-dyy
n,nnode+5,xcirc,ycirc-dyy
n,nnode+6,0,0




!define the circuit elements 

!cond stranded source
cmsel,s,cond
alls,below,area
emodif,all,type,2
emodif,all,real,2
*get,nc1,node,0,nxth
type,5    
real,5
alls
E,nnode+1,nnode+2,nc1




!type,9    !independent voltage source
!real,9
!E,nnode+3,nnode+4,nnode+6

type,3    !independent current source
real,3
E,nnode+3,nnode+4


type,6    !resistor
real,6
E,nnode+4,nnode+5


!cp to connect "wires"
cp,next,volt,nnode+2,nnode+3
cp,next,volt,nnode+1,nnode+5   !order matters if you ground nnode+1



!enforce stranded condition
cmsel,s,cond
alls,below,area
cp,next,emf,all
cp,next,curr,all


nsel,s,node,,nnode+1,nnode+10
cm,gn,node
esln,s,0
cm,ge,elem

alls
/graphics,full
alls
eplot



!******************************************
!********** B.C ***************************
!******************************************

!cp az across coil boundaries
alls
alls,below,area
cpintf,az,1e-7


! Set boundary conditions of the voltage
alls
d,nnode+1,volt,0


! Set boundary conditions of the magnetic vector potential
alls
csys,0
lsel,r,loc,x,0
nsll,s,1
d,all,az,0

alls
csys,1
lsel,r,loc,x,rbc
nsll,s,1
d,all,az,0


!!!!! TURN OFF IRON YOKE
!cmsel,s,yoke
!alls,below,area
!emodif,all,mat,1




!**************************************************
!****** Thermal Model *******
!**************************************************

! thermal for non-cond region
! et,4,plane77



et,12,user101
keyopt,12,1,1   ! use input homoginized properties
keyopt,12,2,1   ! transfer from EMAG with binary
keyopt,12,3,1   ! Nb3Sn superconductor
keyopt,12,4,0   ! copper stabilizer             
keyopt,12,5,0   ! G10 non-cond material
keyopt,12,6,0   ! cucv NIST
keyopt,12,7,0   ! cukxx NIST
keyopt,12,9,0   ! nb3sn cv cudi
keyopt,12,11,0   ! G10 cv nist



!COIL PROPERTIES (mat11)  -> from Lorenzo
mptemp,1,1.9

densmix = 1.0    !since we are working with Cv

cvCu = 500
cvNb3sn = 250
CvG10 = 0.0
CvG10 = 750.0
Cvmix = cvCu*fcond*(1-fsc) + cvNb3sn*fcond*fsc + cvG10*(1-fcond)

kxxCu = 300
kxxNb3Sn = 300
kxxG10 = 0.01
kxxmix = kxxCu*fcond*(1-fsc) + kxxNb3sn*fcond*fsc + kxxG10*(1-fcond)

mp,dens,11,densmix     
mp,kxx,11,kxxmix       
mp,c,11,Cvmix            


!set real constant (only emapping at this point -> to dummy value -> will be matched later)
R,89,999999999999



!generate duplicate mesh for non-air regions - include epoxy
cmsel,s,cond
alls,below,area
agen,2,all,,,,,,,0
cmsel,u,cond
cm,tcond,area
cm,treg,area

cmsel,s,tcond
alls,below,area
emodif,all,mat,11
emodif,all,type,12
emodif,all,real,89


!rename tcond1
cmsel,s,tcond
alls,below,area
csys,0
ksel,r,loc,x,x0-.1e-3,x0+aw+dx+.1e-3
lslk,s,1
asll,s,1
cm,tcond1,area


!**************************************************
!****** Define EMAG/THERMAL Interfaces  ***********
!**************************************************

cmsel,s,cond
cmsel,a,tcond
alls,below,area
bfe,all,fvin,,1     !define interfaces on both elements





!***********************************************************************
!****** strand Element Mapping from Magnetic to Thermal  *******
!***********************************************************************


cmsel,s,cond
alls,below,area
*get,enum,elem,,count

cmsel,s,tcond
alls,below,area
*get,enumtest,elem,,count

!add if to check that they are the same with error exit

*dim,Emap,array,2,enum
*dim,Ecent1,array,3,enum
*dim,Ecent2,array,3,enum
*dim,Et,array,2,enum


cmsel,s,cond
alls,below,area
!place all element #'s from magnetic into array
*do,j,1,enum
 *get,Emap(1,j),elem,0,nxth
 esel,u,elem,,Emap(1,j)
*enddo

!find centroid of each thermal element
*do,j,1,enum
 esel,s,elem,,Emap(1,j)
 *get,Ecent1(1,j),elem,Emap(1,j),cent,x
 *get,Ecent1(2,j),elem,Emap(1,j),cent,y
 *get,Ecent1(3,j),elem,Emap(1,j),cent,z
*enddo



cmsel,s,tcond
alls,below,area
!place all element #'s from thermal into array
*do,j,1,enum
 *get,Et(1,j),elem,0,nxth
 esel,u,elem,,Et(1,j)
*enddo

!find centroid of each magnetic element
*do,j,1,enum
 esel,s,elem,,Et(1,j)
 *get,Ecent2(1,j),elem,Et(1,j),cent,x
 *get,Ecent2(2,j),elem,Et(1,j),cent,y
 *get,Ecent2(3,j),elem,Et(1,j),cent,z
*enddo


!match centroids
*set,tol,1e-7
cnt = 100
*do,j,1,enum
  *do,i,1,enum
	
		*if,abs(Ecent2(1,j)-Ecent1(1,i)),LT,tol,then
			*if,abs(Ecent2(2,j)-Ecent1(2,i)),LT,tol,then
				*if,abs(Ecent2(3,j)-Ecent1(3,i)),LT,tol,then
					r,cnt,Emap(1,i)
					esel,s,elem,,Et(1,i)
					emodif,all,real,cnt
					cnt = cnt + 1
				*endif
			*endif
		*endif
		
	*enddo
*enddo


save,presolve,db





finish
alls
/solu

!mfclear,solu,1
nropt,full

!set initial condition for temp region
cmsel,s,treg
alls,below,area
ic,all,temp,1.9


!MFS solver settings	 
alls
mfan,on ! Activate ANSYS Multi-field solver analysis
!mfel,1,1,2,3,5,6,9   ! Field #1 ET;s, Emag
mfel,1,1,2,3,5,6   ! Field #1 ET;s, Emag
!mfel,2,4,12          ! Field #2 ET's, Thermal
mfel,2,12          ! Field #2 ET's, Thermal
mfor,1,2             ! Field solution order


! load transfer options
mfvo,1,1,hgen,2 ! Transfer hgen from Emag to Thermal
mfvo,1,2,temp,1 ! Transfer Temp from Thermal to Emag



!EMAG analysis options (initial -> before decay)
antype,trans
nlgeom,on
outres,all,all
tref,1.9
pivcheck,off
nsubst,1
!cnvtol,curt,1e-1   !won't converge unless this is set
!cnvtol,vltg,1e-5
!timi,off,mag
!timi,off,elect
mfcm,1,  ! Write Emag analysis options

!Thermal analysis options
antype,trans 
nlgeom,on
tref,1.9
nsubst,1
neqit,50
cnvtol,heat,,,,1e-5
outres,all,last    !only write out final converged temp for each stagger
mfcm,2, ! Write Thermal analysis options


! set it up for no staggering, just passing at a given interval
mfiter,1       !maximum number of stagger iterations, if 1 should set relaxation factor (mfrelax) to 1 (default is 10)
mfrelax,all,1 
!mfrelax,temp,1      !relaxation of transfered loads (default is 0.75)
!mfrelax,hgen,1 
!mfconv,1e-3    !convergence for load transfer (to exit stagger) (default is 1% = 0.01)



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! SOLUTION LOOP
!!  --over LR background field and wire current from Matlab
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

alls
tref,1.9 






!*********************************************
!****** Linear Ramp   ************************
!*********************************************

finish
alls
/solu
outres,all,all
nsubst,1
antype,trans
pivcheck,off
bfunif,temp,1.9

!only output solution for cond and circuit elements for comparison
cmsel,s,cond
cmsel,a,tcond
alls,below,area
cmsel,a,gn
cmsel,a,ge
cm,esolu,elem
cm,nsolu,node
outres,all,none
!outres,esol,all,esolu
!outres,nsol,all,nsolu
outres,esol,last,esolu
outres,nsol,last,nsolu





dt = 0.02
dt = 0.005   !do 4x number of steps
nstp = tf/dt

!nstp = 50

*do,i,1,nstp
 
  !time,dt*i
	
	!SET TIME STEP FROM FILE
	mfti,dt*i   ! End time
	
  !now starts at 2nd second step 				
  mfdt,dt	
	
  alls
  solve
	
*enddo


save


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Post-Process
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
/post26
lines,10000    !avoid repeating headers < 1000 lines
numvar,100    !set number of variables great than the default of 10
In = 10     !start esol numbering at 10

/input,07_ansys_coil_post26_circu_v2_MFS,txt


finish
/post1
file,field1,rst
alls
plnsol,b,y
plnsol,b,sum
csys,0

save

*do,i,1,nstp
	set,i

  /input,001_sum_u102_flux,txt,
  /input,002_sum_nmisc_M_HT0,txt

!	I0%i% = dI*i
!	I0%i% = I0f  !constant current
	flux102sum%i% = flux102sum
!  Lflux%i% = flux102sum/I0%i%
  Lflux%i% = flux102sum
  MXave%i% = Mxave
	Myave%i% = Myave
	Bsave%i% = Bsave
	rhoave%i% = rhoave   !cern homoginized

	PPsumALL%i% = PPsumALL
	PPtotsum%i% = PPtotsum
	PPtausum%i% = PPtausum
	PPtausumALL%i% = PPtausumALL
!  Lflux%i% = Lflux
	flux102sum%i% = flux102sum
	tm%i% = dt*i
	alls
	plnsol,b,sum
	*get,Bmax%i%,plnsol,0,max
*enddo

/output,

/DELETE,Mag_v_t,txt
*cfopen,Mag_v_t,txt
*do,i,1,nstp
	*vwrite,0,flux102sum%i%,Lflux%i%,Bsave%i%,tm%i%,Mxave%i%,Myave%i%,PPtausum%i%,PPtausumALL%i%,PPtotsum%i%,PPsumALL%i%,rhoave%i%
(12(ES16.7))
*enddo
*cfclose





finish
/post1
file,field2,rth
cmsel,s,tcond
alls,below,area
plnsol,temp

csys,0
*do,i,1,nstp
	set,i
	

  /input,002_sum_101_therm,txt
	

	Tsave%i% = Tsave
	tm%i% = dt*i

*enddo


/DELETE,Therm_v_t,txt
*cfopen,Therm_v_t,txt
*do,i,1,nstp
	*vwrite,tm%i%,Tsave%i%
(2(ES16.7))
*enddo
*cfclose





/eof




!*********************************************
!****** Static Solution   ********************
!*********************************************

finish
alls
/solu

time,1
nsubst,1
alls
solve

finish
/post1
alls
plnsol,b,y
plnsol,b,sum
/eof


