!*******************************************************
!********** CERN Benchmarking **************************
!*******************************************************

/title, udf_dipole
/prep7

! Delete binary transfer file if left over

/DELETE,NMISC_trans,txt


!********** Options *******************************
!*afun,rad
pi=acos(-1)
*afun,deg
!/UIS,MSGPOP,4  !suppress warnings
/NERR,,99999999,     ! changes # of error and warnings to prevent crashing for large # of warnings
!**************************************************

/title, udf_dipole


!**************************************************
!****** Material Properties (SI - magnetic) *******
!**************************************************

!Coil
mp,murx,1,1   !rel. perm.
mp,rsvx,1,(1.0e-8)/pi      ! from Lorenzo

!Air
mp,murx,10,1   !rel. perm.

!Iron (4)  bh table from cern
tb,bh,4,, 44
TBPT,,0.000000,0.000000 
TBPT,,7.941832,0.007120 
TBPT,,15.883663,0.016700 
TBPT,,23.825495,0.029250 
TBPT,,31.767326,0.045220 
TBPT,,47.531623,0.088910 
TBPT,,63.367539,0.154340 
TBPT,,79.219371,0.240660 
TBPT,,158.812757,0.765070 
TBPT,,317.649387,1.296830 
TBPT,,475.180947,1.452170 
TBPT,,633.627647,1.520430 
TBPT,,792.106179,1.558410 
TBPT,,1192.094374,1.609540 
TBPT,,1589.758907,1.638920 
TBPT,,1987.081257,1.660550 
TBPT,,2386.281635,1.678260 
TBPT,,2782.290957,1.694270 
TBPT,,3179.493941,1.708280 
TBPT,,3577.039107,1.721750 
TBPT,,3971.377302,1.734140 
TBPT,,4765.210312,1.757060 
TBPT,,5559.099026,1.778050 
TBPT,,6354.069994,1.797640 
TBPT,,7149.486595,1.815670 
TBPT,,7944.083548,1.833300 
TBPT,,10590.000000,1.850000 
TBPT,,13160.000000,1.900000 
TBPT,,21170.000000,2.000000 
TBPT,,33760.000000,2.100000 
TBPT,,66000.000000,2.200000 
TBPT,,120960.000000,2.280000 
TBPT,,169600.000000,2.344300 
TBPT,,212170.000000,2.399600 
TBPT,,283130.000000,2.490500 
TBPT,,339890.000000,2.562700 
TBPT,,425040.000000,2.670600 
TBPT,,566950.000000,2.849800 
TBPT,,850760.000000,3.207400 
TBPT,,1702300.000000,4.278200 
TBPT,,2128000.000000,4.813400 
TBPT,,2837700.000000,5.705200 
TBPT,,3405100.000000,6.418600 
TBPT,,4256700.000000,7.488700
TBPLOT,bh,4,


!**********EM Elements*******************************
et,1,plane53        !2d EM: Az
keyopt,1,1,0    
!et,2,user102   !2d EM: Az,cur,emf (stranded circu)   
!**************************************************

et,2,user102   !2d EM: Az,cur,emf (stranded circu)   
keyopt,2,1,2   ! circuit coupled
keyopt,2,2,1   ! transfer to thermal with binary (0=no,1=yes)
keyopt,2,3,1   ! LBNL Jc fit -> THIS IS NOT YET SET UP -> HARD CODE SCU FIT, key(3) does nothing
               ! no keyopt 4
keyopt,2,5,1   ! rho fit (0=table/fixed, 1=NIST, 2=CUDI, 3=MATPRO, 4=userCu.f)
               ! no keyopt 6
keyopt,2,7,1   ! force qflag at all times (0=check, 1=SC, 2=Quenched) -> real const scIFCU no 18
keyopt,2,8,0   ! IFCC, (0=yes,calc tau; 1=no; 3=yes with fixed tau from real 11)



!/input,02_dipole_geo_twoairbc_dense,txt,
/input,02_dipole_geo_twoairbc_loop,txt,

!name conductor 
cmsel,s,cond1
*do,i,2,nc
	cmsel,a,cond%i%
*enddo
alls,below,area
emodif,all,type,2
emodif,all,real,2




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! real constants
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! scaling to match inductance and resistance -> pick not the same
Lc = 10.11   
Li = 9.20   
     


!set real constants
nturns = nc
nstrand = 40
ds = 0.75e-3
fcond = nstrand*ds*ds*pi/(4*aw*bw)
fsc=0.4
Lp = 14e-3
feff = 1.0
RRR = 200
TauMult = 1.0
scIFCU = 1.0 
curdir = 1.0
Ctau = 1e-3     !if constant tau is chosen using keyopt

! Nb3Sn fit parameters (from Edvard)
Tc0 = 16.0
Bc0 = 28.11
Jc0 = 174000/Bc0
afit=0.96
pfit = 1.52

cmsel,s,cond
alls,below,area
asum
*get,across,area,all,area
		
!no real constants are required for this simulation, put anyways to check they set correct
R,2,across,nturns,fcond,fsc,curdir,Lc   !set real constants
Rmore,Li,RRR,Lp,feff,Ctau,TauMult
Rmore,Jc0,Tc0,Bc0,afit,pfit,scIFCU



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Set Up Circuit
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


et,9,circu124
keyopt,9,1,4        ! circuit element - independent voltage source
keyopt,9,2,4        ! piecewise linear load

et,5,circu124
keyopt,5,1,5        ! circuit element - stranded conductor -> connected to FEA domain

et,6,circu124
keyopt,6,1,0        ! circuit element - external resistor

et,3,circu124
keyopt,3,1,3        ! circuit element - independent current source
keyopt,3,2,4        ! piecewise linear load


!********** Real Constants for Circuit ************************************************************************
rdump = 25.0e-3
I0 = 18e3
tf = 500e-3


SymFac = 4  ! left/right,top/bot

R,3,I0,0.0

R,5,1.0     !symfactor is 1 (no symmetry)
R,5,SymFac   
R,6,rdump
!*set,Rtot,rdump+SymFac*2*lmag*rhoc/(across)
!*set,Rtot,rdump+2*lmag*rhoc/(across)
*set,Rtot,rdump
R,9,0,I0*RTot,5.0/1000,I0*RTot,5.1/1000,0,tf,0    ! "dump res switch" 


!set ramp parameters 
!tf = 1.0
!I0f = 1.0e2
!R,3,0,0,tf,I0f    ! "linear ramp of current to I0f"




!**************************************************
!********** Create Circuit  ***********************
!**************************************************
csys,0

xcirc = x0
ycirc = -20e-3
dyy = 20e-3
xsft = aw*nc+dx*nc

!make circuit nodes (location is arbitrary, connectivity is important)
alls
numcmp,node
*get,nnode,node,,num,max
numstr,node,nnode
n,nnode+1,xcirc,ycirc
n,nnode+2,xcirc+xsft,ycirc
n,nnode+3,xcirc+xsft,ycirc-dyy
n,nnode+4,xcirc+xsft/2.0,ycirc-dyy
n,nnode+5,xcirc,ycirc-dyy
n,nnode+6,0,0




!define the circuit elements 

!cond stranded source
cmsel,s,cond
alls,below,area
emodif,all,type,2
emodif,all,real,2
*get,nc1,node,0,nxth
type,5    
real,5
alls
E,nnode+1,nnode+2,nc1




type,9    !independent voltage source
real,9
E,nnode+3,nnode+4,nnode+6    !results in pos. field


!type,3    !independent current source
!real,3
!E,nnode+3,nnode+4


type,6    !resistor
real,6
E,nnode+4,nnode+5


!cp to connect "wires"
cp,next,volt,nnode+2,nnode+3
cp,next,volt,nnode+1,nnode+5   !order matters if you ground nnode+1



!enforce stranded condition
cmsel,s,cond
alls,below,area
cp,next,emf,all
cp,next,curr,all


nsel,s,node,,nnode+1,nnode+10
cm,gn,node
esln,s,0
cm,ge,elem

alls
/graphics,full
alls
eplot




!******************************************
!********** B.C ***************************
!******************************************

!cp az across coil boundaries
alls
alls,below,area
cpintf,az,1e-7


! Set boundary conditions of the voltage
alls
d,nnode+1,volt,0


! Set boundary conditions of the magnetic vector potential
alls
csys,0
lsel,r,loc,x,0
nsll,s,1
d,all,az,0

alls
csys,1
lsel,r,loc,x,rbc
nsll,s,1
d,all,az,0


!!!!! TURN OFF IRON YOKE
!cmsel,s,yoke
!alls,below,area
!emodif,all,mat,1




!**************************************************
!****** Thermal Model *******
!**************************************************

! thermal for non-cond region
! et,4,plane77

!COIL PROPERTIES (mat11)  -> fake to avoid error of undefined
mptemp,1,1.9      
mp,dens,11,99999    
mp,kxx,11,99999        
mp,c,11,99999  



! thermal for conductor region only
et,12,user101
keyopt,12,1,0         ! 0=internal fits, 1=ANSYS table
keyopt,12,2,1         ! 0=no transfer to mag, 1=transfer to mag
keyopt,12,3,1         ! 0=NbTi, 1=Nb3Sn, 2 = Bi2212
keyopt,12,4,0         ! 0=Cu, 1=Ag
keyopt,12,5,0         ! 0=G10
keyopt,12,6,1         ! 0=NIST Cucv, 1=CUDI, 2=MATPRO 
keyopt,12,7,0         ! 0=NIST Cukxx, 1=CUDI, 2=MATPRO 
keyopt,12,8,0         ! TBD (NbTi Cv)
keyopt,12,9,0         ! 0=NIST, 1=CUDI,  (Nb3Sn Cv)
keyopt,12,10,0         ! TBD  (Bi2212 Cv)
keyopt,12,11,0         ! 0=NIST  (G10 Cv)
keyopt,12,12,0         ! Agcv
keyopt,12,13,0         ! Agkxx


!set real constant (only emapping at this point -> to dummy value -> will be matched later)
R,89,999999999999



!generate duplicate mesh for non-air regions - include epoxy
cmsel,s,cond
alls,below,area
agen,2,all,,,,,,,0
cmsel,u,cond
cm,tcond,area
cm,treg,area

cmsel,s,tcond
alls,below,area
emodif,all,mat,11
emodif,all,type,12
emodif,all,real,89


!rename tcond1
cmsel,s,tcond
alls,below,area
csys,0
ksel,r,loc,x,x0-.1e-3,x0+aw+dx+.1e-3
lslk,s,1
asll,s,1
cm,tcond1,area


!**************************************************
!****** Define EMAG/THERMAL Interfaces  ***********
!**************************************************

cmsel,s,cond
cmsel,a,tcond
alls,below,area
bfe,all,fvin,,1     !define interfaces on both elements





!***********************************************************************
!****** strand Element Mapping from Magnetic to Thermal  *******
!***********************************************************************


cmsel,s,cond
alls,below,area
*get,enum,elem,,count

cmsel,s,tcond
alls,below,area
*get,enumtest,elem,,count

!add if to check that they are the same with error exit

*dim,Emap,array,2,enum
*dim,Ecent1,array,3,enum
*dim,Ecent2,array,3,enum
*dim,Et,array,2,enum


cmsel,s,cond
alls,below,area
!place all element #'s from magnetic into array
*do,j,1,enum
 *get,Emap(1,j),elem,0,nxth
 esel,u,elem,,Emap(1,j)
*enddo

!find centroid of each thermal element
*do,j,1,enum
 esel,s,elem,,Emap(1,j)
 *get,Ecent1(1,j),elem,Emap(1,j),cent,x
 *get,Ecent1(2,j),elem,Emap(1,j),cent,y
 *get,Ecent1(3,j),elem,Emap(1,j),cent,z
*enddo



cmsel,s,tcond
alls,below,area
!place all element #'s from thermal into array
*do,j,1,enum
 *get,Et(1,j),elem,0,nxth
 esel,u,elem,,Et(1,j)
*enddo

!find centroid of each magnetic element
*do,j,1,enum
 esel,s,elem,,Et(1,j)
 *get,Ecent2(1,j),elem,Et(1,j),cent,x
 *get,Ecent2(2,j),elem,Et(1,j),cent,y
 *get,Ecent2(3,j),elem,Et(1,j),cent,z
*enddo


!match centroids
*set,tol,1e-7
cnt = 100
*do,j,1,enum
  *do,i,1,enum
	
		*if,abs(Ecent2(1,j)-Ecent1(1,i)),LT,tol,then
			*if,abs(Ecent2(2,j)-Ecent1(2,i)),LT,tol,then
				*if,abs(Ecent2(3,j)-Ecent1(3,i)),LT,tol,then
					r,cnt,Emap(1,i)
					esel,s,elem,,Et(1,i)
					emodif,all,real,cnt
					cnt = cnt + 1
				*endif
			*endif
		*endif
		
	*enddo
*enddo


save,presolve,db


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Begin Solution 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

finish
alls
/solu

!mfclear,solu,1
nropt,full

!set initial condition for temp region
cmsel,s,treg
alls,below,area
ic,all,temp,4.5


!only output solution for cond and circuit elements for comparison
cmsel,s,cond
alls,below,area
cmsel,a,gn
cmsel,a,ge
cm,esolu,elem
cm,nsolu,node

cmsel,s,tcond
alls,below,area
cm,esoluT,elem
cm,nsoluT,node

outres,all,none
outres,esol,all,esolu
outres,nsol,all,nsolu
outres,esol,last,esoluT
outres,nsol,last,nsoluT


!MFS solver settings	 
alls
mfan,on ! Activate ANSYS Multi-field solver analysis
mfel,1,1,2,5,6,9   ! Field #1 ET;s, Emag
!mfel,1,1,2,3,5,6   ! Field #1 ET;s, Emag
!mfel,2,4,12          ! Field #2 ET's, Thermal
mfel,2,12          ! Field #2 ET's, Thermal
mfor,1,2             ! Field solution order


! load transfer options
mfvo,1,1,hgen,2 ! Transfer hgen from Emag to Thermal
mfvo,1,2,temp,1 ! Transfer Temp from Thermal to Emag



!EMAG analysis options (initial -> before decay)
antype,trans
nlgeom,on
outres,all,all
tref,4.5
pivcheck,off
nsubst,1
cnvtol,curt,,,,1e-6   !won't converge unless this is set
cnvtol,vltg,,,,1e-6
timi,off,mag
autos,off
nsubst,1
mfcm,1,  ! Write Emag analysis options

!Thermal analysis options
antype,trans 
nlgeom,on
tref,4.5
nsubst,1
neqit,50
!cnvtol,heat,,,,1e-5
outres,all,last    !only write out final converged temp for each stagger
mfcm,2, ! Write Thermal analysis options


! set it up for no staggering, just passing at a given interval
mfiter,1       !maximum number of stagger iterations, if 1 should set relaxation factor (mfrelax) to 1 (default is 10)
mfrelax,all,1 
!mfrelax,temp,1      !relaxation of transfered loads (default is 0.75)
!mfrelax,hgen,1 
!mfconv,1e-3    !convergence for load transfer (to exit stagger) (default is 1% = 0.01)



!solve LR decay
/title,Test4D



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Begin Solution Loop
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!solve with no timi up to point at which voltage decays (with no quench real 18=0)
alls
taumult = 0.0
rmodif,2,12,taumult
!nsubst,5
mfti,5e-3        ! End time		
mfdt,(5e-3)/5.0  ! dt	
solve



!turn on transient effects
!re-do thermal sol commands
timi,on,all
!nsubst,10
autos,on 
deltim,1e-5,1e-5,1e-4,on 
nropt,full
!cnvtol,heat,1e-1
!cnvtol,temp,1e-3
neqit,50
mfcm,2, ! Write Thermal analysis options
			
!re-do EMAG sol commands			
timi,on,all
autos,off
nsubst,1
mfcm,1, ! Write magnetic analysis options


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!			
! solve up to 5.5 ms with tau ramping up
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
ti = 5.1e-3 + 1e-5
tf = 5.5e-3
dt = 1e-5
nstp = (tf-ti)/dt + 1.0
aa = 2.0    !power for transistion

*do,i,1,nstp
	tt = ti+(i-1)*dt
	TauMult = ((tt-ti)/(tf-ti))**aa         !this could be implemented for turning off/on with change in quench state, where tf-ti is time const and tf and ti are saved var
	rmodif,2,12,TauMult
	alls
	mfti,tt    ! End time		
	mfdt,dt   ! dt	
	nsubst,1
	solve
*enddo





!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!			
! solve up to 10.0 ms 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
dt = 1e-4
ti = tf + dt
tf = 10.0e-3
nstp = (tf-ti)/dt + 1.0

!as a precaution 
TauMult = 1.0      
rmodif,2,12,TauMult
	
*do,i,1,nstp
	tt = ti+(i-1)*dt
	alls
	mfti,tt    ! End time		
	mfdt,dt   ! dt	
	nsubst,1
	solve
*enddo



!re-do thermal sol commands
timi,on,all
autos,on 
deltim,1e-4,1e-4,0.5e-3,on 
nropt,full
neqit,50
mfcm,2, ! Write Thermal analysis options


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!			
! solve up to 50.0 ms 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
dt = 0.5e-3
ti = tf + dt
tf = 50.0e-3
nstp = (tf-ti)/dt + 1.0

	
*do,i,1,nstp
	tt = ti+(i-1)*dt
	alls
	mfti,tt    ! End time		
	mfdt,dt   ! dt	
	nsubst,1
	solve
*enddo




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!			
! solve up to 150.0 ms 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
dt = 2.0e-3
ti = tf + dt
tf = 150.0e-3
nstp = (tf-ti)/dt + 1.0

	
*do,i,1,nstp
	tt = ti+(i-1)*dt
	alls
	mfti,tt    ! End time		
	mfdt,dt   ! dt	
	nsubst,1
	solve
*enddo


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!			
! solve up to 500.0 ms 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
dt = 3.0e-3
ti = tf + dt
tf = 498.0e-3
nstp = (tf-ti)/dt + 1.0

*do,i,1,nstp
	tt = ti+(i-1)*dt
	alls
	mfti,tt    ! End time		
	mfdt,dt   ! dt	
	nsubst,1
	solve
*enddo



save





!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Time-Hist Post-Process
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/post26
lines,10000    !avoid repeating headers < 1000 lines
numvar,100    !set number of variables great than the default of 10
In = 10     !start esol numbering at 10
/input,07_ansys_coil_post26_circu_v2,txt



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Post1 Loop
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


finish
/post1
file,field1,rst
cmsel,s,cond
set,last,
*get,nset,active,0,set,NSET
plnsol,b,y
plnsol,b,sum
csys,0



/output,

!find the yoke energy for the first and last step
/uis,msgpop,4     !only error msg
set,first
cmsel,s,yoke
alls,below,area
senergy,0
eyokeI = S_ENG
set,last
cmsel,s,yoke
alls,below,area
senergy,0
eyokeF = S_ENG





/DELETE,Mag_v_t,txt
*cfopen,Mag_v_t,txt

set,first
*do,i,1,nset
  /input,002_sum_nmisc_M_HT0,txt
!	EngA = eother + eyoke%i%
	EngA = eother 
  *get,tm,active,0,set,time
	*vwrite,tm,flux102sum,EngA,eyokeI,eyokeF,tauave,Mxave,Myave,PPtausum,PPtausumALL,
(10(ES16.7))
	set,next
*enddo

*cfclose



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Post1 Loop Thermal
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


finish
/post1
file,field2,rst
cmsel,s,tcond
set,last,
*get,nset,active,0,set,NSET
plnsol,b,y
plnsol,b,sum
csys,0

/DELETE,Therm_v_t,txt
*cfopen,Therm_v_t,txt

set,first
*do,i,1,nset
  /input,002_sum_101_therm,txt
  *get,tm,active,0,set,time
	*vwrite,tm,Tsave,Bsave,CvCuave,CvNb3Snave,CvG10ave,Cvmixave
(10(ES16.7))
	set,next
*enddo

*cfclose



